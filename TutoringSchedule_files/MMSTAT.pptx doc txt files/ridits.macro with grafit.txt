%macro ridits(DataFile,nogrp,codeno,mtitle,one);
/****************************************************************************
*  RIDIT ANALYSIS written while at the U.S. EPA  July 22,1995               *
*                  modified June, 1996,1997                                 *
*                                                                           *
*  Author: Mary A. Marion                                                   *
*                                                                           *
*  Inputs:                                                                  *
*                                                                           *
*  NOGRP  = Number of groups                                                *
*  CODENO = No of severity codes (levels)                                   *
*  MTITLE = title of the experiment                                         *
*  DSNAME = Input data matrix of severity codes by group                    *
*  ONE    = reference population                                            *
*                                                                           *
*  Constraints:                                                             *
*                                                                           *
*  Scheffe'-type comparisons between groups always compare to Grp1.         *
*  Thus always enter the control group as Grp1 when not combining across    *
*  all groups to form the reference population                              *
*                                                                           *
*  Macros called:  compgrp, dosums, equalmns, group, interval, mmeans2,     *
*                  myprints                                                 *
*                                                                           *
************************************************************************** */
data timetrak;
time1=time();

data DataFile; set &DataFile;
one=&one;
two=one/2;

/* THREE (Column 3) Computation */
proc transpose data=DataFile out=td; var one; run;
%dosums(&codeno);
proc transpose data=td out=td2; var sum1-sum&codeno; run;
data ridit; merge td2 DataFile; rename col1=three;
keep severity one two col1 dum Grp1-Grp&nogrp;
dum=1;
run;

/* RIDIT Calculations */
%mmeans2(ridit,one,sum);
   data ridit; merge ridit meansout; by dum;
   data ridit; set ridit; drop sum;
   sum0=sum;
four=two+three;
ridit=four/sum0;

/* GROUP CALCULATIONS */
%group;

/* OUTPUT of table of dose group X severity levels +
   ridits for the severity categories                */
data _null_; file print; put _page_; run;
options formdlim='';
options nonumber;
proc print data=ridit noobs;
   var severity grp1-grp&nogrp one ridit;
   sum grp1-grp&nogrp one;
   title &mtitle; run;

/* OUTPUT of rbars, Population Mean Ridit and standard errors of rbars */
data riditout; set ridit; if _n_ > 1 then delete;
options formdlim=' ';
title;
proc print data=riditout noobs;
  var rbar1-rbar&nogrp PopRidit;
  run;
proc print data=riditout noobs;
  var std1-std&nogrp;
  run;

/* CONFIDENCE INTERVALS on the RIDIT MEANS */
%interval(&nogrp);

/* TESTING the HYPOTHESIS of EQUAL MEAN RIDITS */
%equalmns(&nogrp);

/* (G-1) SCHEFFE`-type GROUP COMPARISONS to the control group (Grp1) */
     %macro generate(nogrp);
        %do i=2 %to &nogrp;
        %compgrp(&i); %end;
        %mend;
     %generate(&nogrp);

/* Table of Confidence Intervals and Odds */
data interval; set interval;
file print;
put "           95% Simultaneous Confidence Intervals on Mean Ridits" /;
put "             "
    "Group"  "       Bonferonni"  "         Scheffe`"  "      Odds";
%do i=2 %to &nogrp;
   ii=&i;
   put "             "     ii 3.0  "  "
       BonL&i   10.3  ","  BonU&i   6.3
       SchefL&i 10.3  ","  SchefU&i 6.3
       odds&i 08.2   ":1";
%end;
run;

/* Graphical Analysis */
%grafit(&nogrp);

/* Output of Intermediate Calculations */
/* %myprints; */

title1; title2; title3;
data timetrak; set timetrak;
time2=time();
Xtime=(time2-time1)/60;
file print;
put _page_ ;
put // "Total Execution Time is " xtime 5.3 " Minutes";

proc datasets;
delete DataFile td td2 meansout ridit riditout fij scheffe
       f equalmns interval grafit timetrak;
run;


%mend ridits;



%macro compgrp(compgrp);
options nosource nomprint;
/* TIE Group and Fij Statistic Calculations */
/* Fleiss, Design and Analysis of Clinical Experiments page 77 (3.37) */

data fij; set ridit;
keep dum tie ndot fnum1 fdenom1 sum1-sum&nogrp ndot rbar1-rbar&nogrp ;
tie=grp1+grp&compgrp;
ndot=sum1+sum&compgrp;
fnum1=tie*(tie-1)*(tie+1);
fdenom1=ndot*(ndot-1)*(ndot+1);

%mmeans2(fij,fnum1,sum);
data fij; merge fij meansout; by dum; drop sum;
fnum=sum;

%mmeans2(fij,fdenom1,max);
data fij; merge fij meansout; by dum; drop max;
fdenom=max;
Fij=1-(fnum/fdenom);

data scheffe; set fij;
if _N_ > 1 then delete;
/* Fleiss, Design and Analysis of Clinical Experiments page 82 (3.43) */
above=12 * sum1 * sum&compgrp * (rbar1-rbar&compgrp)**2;
below = ( sum1 + sum&compgrp + 1) * Fij;
Chi=above/below;
df=&nogrp-1;
p_value=1-probchi(Chi,df);

/* OUTPUT of Scheffe ChiSquare */
title;
proc print data=scheffe split='*' noobs; var Chi df p_value;
label Chi="Scheffe`*ChiSquare" df='Degrees*of*Freedom' p_value='p_value' ;
title "Group1 vs Group&compgrp"; run;
title;

%mend compgrp;


%macro dosums(codeno);
options source mprint symbolgen;
%local i ii stop;
data td; set td; keep sum1-sum&codeno;
sum1=0;
sum2=sum(col1);
%let stop=&codeno+1;
%do i=3 %to &stop;
   %do ii=&i-2 %to &i-2; %end;
   sum&i=sum(of col1-col&ii);
%end;
options source mprint;
%mend dosums;


%macro equalmns(nogrp);
options nosource nomprint;
/* TIE Group and F Statistic Calculations */
/* Fleiss, Design and Analysis of Clinical Experiments
      page 77 (3.37) and page 82 (3.42) */

data f; set ridit;
keep dum tie ndot fnum1 fdenom1 sum1-sum&nogrp
     ndot rbar1-rbar&nogrp PopRidit tot;
tie=sum(of grp1-grp&nogrp);
fnum1=tie*(tie-1)*(tie+1);
fdenom1=ndot*(ndot-1)*(ndot+1);
%do j=1 %to &nogrp;
comp&j = sum&j * (rbar&j-PopRidit)**2;
%end;
tot=sum(of comp1-comp&nogrp);

%mmeans2(f,fnum1,sum);
data f; merge f meansout; by dum; drop sum;
fnum=sum;

%mmeans2(f,fdenom1,max);
data f; merge f meansout; by dum; drop max;
fdenom=max;
F=1-(fnum/fdenom);

data equalmns; set f;
if _N_ > 1 then delete;
/* Fleiss, Design and Analysis of Clinical Experiments page 82 (3.42) */
above=12 * ndot * tot;
below = ( ndot + 1) * F;
Chi=above/below;
df=&nogrp-1;
p_value=1-probchi(Chi,df);

/* OUTPUT of Scheffe` Test of Equal Means ChiSquare */
data _null_; file print;
   put "                             Scheffe` Analysis";
   run;
proc print data= equalmns split='*' noobs; var Chi df p_value;
   label Chi="ChiSquare" df='Degrees*of*Freedom' p_value='p_value' ;
   title "Test of Equal Mean Ridits"; run;

%mend equalmns;


%macro grafit(nogrp);
data grafit; set interval; keep rbarL rbar rbarU group;
%local i;
%do i=1 %to &nogrp;
   rbarL=usualL&i;
   rbar=rbar&i;
   rbarU=usualU&i;
   Group=&i;
   output;
%end;
proc print data=grafit noobs;
  title "grafit";
  run cancel;
goptions reset=(symbol axis footnote legend);
proc gplot data=grafit;
  title1 h=2   "&mtitle";
  title2 h=1.5 "Ridit Analysis";
  title3 h=1.5 "Usual 95% Confidence Limits";
  plot (rbarL rbar rbarU) * group / haxis=axis1 vaxis=axis2 overlay;
  symbol1 v=diamond l=2 c=blue;
  symbol2 v=dot     l=2 c=red;
  symbol3 v=square  l=2 c=green;
  axis1 minor=none /* order = 0 to 5 by 1 */
        offset=(2,2);
  axis2 minor=none order = 0 to 1 by .10
        label=(a=-90 r=90 f=complex c=black "MeanRidit");
run;
goptions reset=(symbol axis footnote legend);
%mend grafit;


%macro group;
%local j;
%do j=1 %to &nogrp;
%mmeans2(ridit,Grp&j,sum);
   data ridit; merge ridit meansout; by dum;
   data ridit; set ridit; drop sum;
   sum&j=sum;
   product&j=Grp&j*ridit;
%mmeans2(ridit,product&j,sum);
   data ridit; merge ridit meansout; by dum;
   data ridit; set ridit; drop sum;
   sumprod&j=sum;
   rbar&j=sumprod&j/sum&j;
   std&j=1/(2*sqrt(3*sum&j));
   wtrbar&j=sum&j*rbar&j;
%end;
data ridit; set ridit;
  ndot=sum(of sum1-sum&nogrp);
  PopRidit=sum(of wtrbar1-wtrbar&nogrp)/ndot;
%mend group;


%macro interval(nogrp);
/* options source mprint symbolgen; */

data interval; set ridit;
%local i;
quantile=probit(.975);
%do i=1 %to &nogrp;
   diff&i   = 1-rbar&i;
   sqval1&i = (rbar&i * diff&i) / ( sum&i-1 );
   sqval&i  = sqrt(sqval1&i);
   usual&i  = quantile * sqval&i;
   usualL&i = rbar&i - usual&i;
   usualU&i = rbar&i + usual&i;
   roughL&i = rbar&i - 1 / sqrt(3*sum&i);
   roughU&i = rbar&i + 1 / sqrt(3*sum&i);
   odds&i   = ( 0.5 + ( rbar&i-rbar1 ) ) /
        ( 1 - ( 0.5 + (rbar&i-rbar1) ) );
%end;

data interval; set interval; if _n_ > 1 then delete;
keep rbar1-rbar&nogrp diff1-diff&nogrp
     usualL1-usualL&nogrp usualU1-usualU&nogrp
     roughL1-roughL&nogrp roughU1-roughU&nogrp
     odds1-odds&nogrp quantile sum1-sum&nogrp;

data interval; set interval;
%local i;
%do i=2 %to &nogrp;
   num&i = sqrt(sum&i+sum1);
   den&i = 2*sqrt(3*sum&i*sum1);
   se&i  =  num&i/den&i;
   paramter = 1- (.05 / (2* (&nogrp-1) ) );
   B = probit(paramter);
   paramter =  &nogrp-1;
   S = cinv(.975, paramter);
   BonL&i   = rbar&i-rbar1+.5 - B*se&i;
   BonU&i   = rbar&i-rbar1+.5 + B*se&i;
   SchefL&i = rbar&i-rbar1+.5 - S*se&i;
   SchefU&i = rbar&i-rbar1+.5 + S*se&i;
%end;

data interval; set interval; file print;
    put "         "
        "    95% Confidence Intervals on Individual Mean Ridits" /;
    put "         " "Group" "      Rbar   "
                     "        ROUGH     " "      USUAL"
        "      Odds" / ;
    %do i= 1 %to &nogrp;
    ii=&i;
    put "         " ii 5.0
        rbar&i   10.3
        roughL&i 09.3 "," roughU&i 06.3
        usualL&i 09.3 "," usualU&i 06.3
        odds&i   08.2 ":1";
    %end;
run;

%mend interval;


%macro mmeans2(dsname,varlst,stat);
options nosource nomprint;
proc datasets library=work; delete meansout; run;
proc means data=&dsname noprint;
  var &varlst;
  output out=meansout
     n=n nmiss=nmiss mean=mean std=std min=min max=max range=range
     sum=sum var=var uss=uss css=css stderr=stderr cv=cv
/*   skewness=skewness kurtosis=kurtosis sumwgt=sumwgt */
     t=t prt=prt;
run;
data meansout; set meansout;
dum=1;
keep &stat dum; run;
options source mprint;
%mend mmeans2;


%macro myprints;
options formdlim='';
title1;
data _null_; file print; put _page_; run;

title1 &mtitle;
proc print data=ridit noobs;
var severity Grp1-Grp&nogrp one two three four ridit
    product1-product&nogrp sum1-sum&nogrp sumprod1-sumprod&nogrp
    rbar1-rbar&nogrp std1-std&nogrp wtrbar1-wtrbar&nogrp ndot PopRidit;
    title2 "Calculations -&nogrp Groups"; run;

options formdlim='';
title1 &mtitle;
proc print data=fij; title2 'fij'; run;
proc print data=scheffe noobs; title2 "scheffe`"; run ;

options formdlim='';
title1 &mtitle;
proc print data=f noobs;        title2 'f'       ; run ;
proc print data=equalmns noobs; title2 "equalmns"; run ;
proc print data=interval noobs;
var  rbar1-rbar&nogrp diff1-diff&nogrp
     usualL1-usualL&nogrp usualU1-usualU&nogrp
     roughL1-roughL&nogrp roughU1-roughU&nogrp
     odds1-odds&nogrp quantile sum1-sum&nogrp
     num2-num&nogrp den2-den&nogrp se2-se&nogrp;
title2 "interval"; run;

%mend myprints;


